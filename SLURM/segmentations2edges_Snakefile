# Snakemake workflow for labels_to_contours processing
# Run with: ./segmentations2edges.sh
# Or with dry-run: ./segmentations2edges.sh --dry-run

import glob
import os

INPUT_DIR = r'/users/kir-fritzsche/aif490/devel/tissue_analysis/lymphnode_analysis/data2track/b2-2a_2c_pos6-01_deskew_cgt/crop3/segmentation'
OUTPUT_DIR = r'/users/kir-fritzsche/aif490/devel/tissue_analysis/lymphnode_analysis/data2track/b2-2a_2c_pos6-01_deskew_cgt/crop3/segmentation/edges_foregrounds'
OUTPUT_DIR_COMBINED = OUTPUT_DIR + '/combined'
OUTPUT_DIR_ZARR = OUTPUT_DIR_COMBINED + '/zarr'
EDGES_SIGMA = 1.0
FOREGROUND_SIGMA = 0.0

# Count all segmentation files in directory and subdirectories using os.walk
seg_files = []
for root, dirs, files in os.walk(INPUT_DIR):
    for file in files:
        if (file.endswith('.tif') or file.endswith('.tiff')) and '_segmented' in file and not '_foreground' in file and not '_edges' in file and not 'videos' in file:
            seg_files.append(os.path.join(root, file))

seg_files = sorted(seg_files)
NUM_FILES = len(seg_files)

# Extract base names for output files (without extension)
SEG_BASENAMES = [os.path.splitext(os.path.basename(f))[0] for f in seg_files]

# Extract unique timepoints from filenames (format: *_timepoint_0001_*.tif)
timepoints = set()
for basename in SEG_BASENAMES:
    if '_timepoint_' in basename:
        # Find the position after '_timepoint_'
        idx = basename.find('_timepoint_') + len('_timepoint_')
        # Extract the next 4 digits
        timepoint_str = basename[idx:idx+4]
        if timepoint_str.isdigit():
            timepoints.add(int(timepoint_str))

timepoints = sorted(list(timepoints))

print(f"Found {NUM_FILES} segmentation files to process")
print(f"Found {len(timepoints)} unique timepoints: {timepoints}")
if NUM_FILES > 0:
    print(f"First file: {seg_files[0]}")
    print(f"Last file: {seg_files[-1]}")

# Final outputs - all foreground and edges files
rule all:
    input:
        expand(OUTPUT_DIR + "/{basename}_foreground.tif", basename=SEG_BASENAMES),
        expand(OUTPUT_DIR + "/{basename}_edges.tif", basename=SEG_BASENAMES),
        expand(OUTPUT_DIR_COMBINED + "/combined_edges_blur_s{sigma}_t{timepoint}.tif", sigma=float(EDGES_SIGMA), timepoint=[f"{t:04d}" for t in timepoints]),
        expand(OUTPUT_DIR_COMBINED + "/combined_foreground_blur_s{sigma}_t{timepoint}.tif", sigma=float(FOREGROUND_SIGMA), timepoint=[f"{t:04d}" for t in timepoints]),
        OUTPUT_DIR_ZARR + "/edges_blur_s" + str(float(EDGES_SIGMA)) + ".zarr",
        OUTPUT_DIR_ZARR + "/foreground_blur_s" + str(float(FOREGROUND_SIGMA)) + ".zarr"

# Rule: Process individual segmentation files (parallel processing)
rule labels_to_contours:
    output:
        foreground = OUTPUT_DIR + "/{basename}_foreground.tif",
        edges = OUTPUT_DIR + "/{basename}_edges.tif"
    params:
        seg_path = lambda wildcards: seg_files[SEG_BASENAMES.index(wildcards.basename)]
    threads: 1
    resources:
        mem_mb = 64000,
        runtime = 15,
        slurm_partition = "short",
        slurm_extra = "--exclude=compg009,compg010,compg011,compg013"
    log:
        "slogs/labels_to_contours_{basename}.log"
    shell:
        """
        module purge
        source /well/kir/config/modules.sh
        module load Python/3.10.8-GCCcore-12.2.0
        source ~/devel/venv/Python-3.10.8-GCCcore-12.2.0/ultrack_env/bin/activate
        
        python3 -u /users/kir-fritzsche/aif490/devel/tissue_analysis/lymphnode_analysis/src/utils/batch_labels_to_contours.py \
            --seg_path "{params.seg_path}" \
            --output_dir {OUTPUT_DIR}  &> {log}
        """

#Rule 2: Combine edges across timepoints by parent directory
rule combine_edges_byparent:
    input:
        lambda wildcards: [OUTPUT_DIR + f"/{basename}_edges.tif" for basename in SEG_BASENAMES 
                          if f"_timepoint_{wildcards.timepoint}_" in basename]
    output:
        OUTPUT_DIR_COMBINED + "/combined_edges_blur_s" + str(float(EDGES_SIGMA)) + "_t{timepoint}.tif"
    params:
        timepoint = lambda wildcards: int(wildcards.timepoint),
        sigma = EDGES_SIGMA
    threads: 1
    resources:
        mem_mb = 64000,
        runtime = 30,
        slurm_partition = "short",
        slurm_extra = "--exclude=compg009,compg010,compg011,compg013"
    log:
        "slogs/combine_edges_t{timepoint}.log"
    shell:
        """
        module purge
        source /well/kir/config/modules.sh
        module load Python/3.10.8-GCCcore-12.2.0
        source ~/devel/venv/Python-3.10.8-GCCcore-12.2.0/ultrack_env/bin/activate
        
        python3 -u /users/kir-fritzsche/aif490/devel/tissue_analysis/lymphnode_analysis/src/utils/combine_edges.py \
            --input_dir {OUTPUT_DIR} \
            --output_dir {OUTPUT_DIR_COMBINED} \
            --timepoint {params.timepoint} \
            --sigma {params.sigma} &> {log}
        """


#Rule 3: Combine foreground across timepoints by parent directory
rule combine_foreground_byparent:
    input:
        lambda wildcards: [OUTPUT_DIR + f"/{basename}_foreground.tif" for basename in SEG_BASENAMES 
                          if f"_timepoint_{wildcards.timepoint}_" in basename]
    output:
        OUTPUT_DIR_COMBINED + "/combined_foreground_blur_s" + str(float(FOREGROUND_SIGMA)) + "_t{timepoint}.tif"
    params:
        sigma = FOREGROUND_SIGMA,
        timepoint = lambda wildcards: int(wildcards.timepoint)
    threads: 1
    resources:
        mem_mb = 64000,
        runtime = 30,
        slurm_partition = "short",
        slurm_extra = "--exclude=compg009,compg010,compg011,compg013"
    log:
        "slogs/combine_foreground_t{timepoint}.log"
    shell:
        """
        module purge
        source /well/kir/config/modules.sh
        module load Python/3.10.8-GCCcore-12.2.0
        source ~/devel/venv/Python-3.10.8-GCCcore-12.2.0/ultrack_env/bin/activate
        
        python3 -u /users/kir-fritzsche/aif490/devel/tissue_analysis/lymphnode_analysis/src/utils/combine_foreground.py \
            --input_dir {OUTPUT_DIR} \
            --output_dir {OUTPUT_DIR_COMBINED} \
            --sigma {FOREGROUND_SIGMA} \
            --timepoint {params.timepoint} &> {log}
        """

#Rule 4: Convert combined edges TIF files to zarr
rule convert_edges_to_zarr:
    input:
        expand(OUTPUT_DIR_COMBINED + "/combined_edges_blur_s{sigma}_t{timepoint}.tif", sigma=EDGES_SIGMA, timepoint=[f"{t:04d}" for t in timepoints])
    output:
        directory(OUTPUT_DIR_ZARR + "/edges_blur_s" + str(EDGES_SIGMA) + ".zarr")
    threads: 1
    resources:
        mem_mb = 128000,
        runtime = 30,
        slurm_partition = "short",
        slurm_extra = "--exclude=compg009,compg010,compg011,compg013"
    log:
        "slogs/convert_edges_to_zarr.log"
    shell:
        """
        module purge
        source /well/kir/config/modules.sh
        module load Python/3.10.8-GCCcore-12.2.0
        source ~/devel/venv/Python-3.10.8-GCCcore-12.2.0/ultrack_env/bin/activate
        
        python3 -u /users/kir-fritzsche/aif490/devel/tissue_analysis/lymphnode_analysis/src/utils/tif2zarr.py \
            --input_dir {OUTPUT_DIR_COMBINED} \
            --zarr_path {output} \
            --filter edges &> {log}
        """

#Rule 5: Convert combined foreground TIF files to zarr
rule convert_foreground_to_zarr:
    input:
        expand(OUTPUT_DIR_COMBINED + "/combined_foreground_blur_s{sigma}_t{timepoint}.tif", sigma=FOREGROUND_SIGMA, timepoint=[f"{t:04d}" for t in timepoints])
    output:
        directory(OUTPUT_DIR_ZARR + "/foreground_blur_s" + str(FOREGROUND_SIGMA) + ".zarr")
    threads: 1
    resources:
        mem_mb = 128000,
        runtime = 30,
        slurm_partition = "short",
        slurm_extra = "--exclude=compg009,compg010,compg011,compg013"
    log:
        "slogs/convert_foreground_to_zarr.log"
    shell:
        """
        module purge
        source /well/kir/config/modules.sh
        module load Python/3.10.8-GCCcore-12.2.0
        source ~/devel/venv/Python-3.10.8-GCCcore-12.2.0/ultrack_env/bin/activate
        
        python3 -u /users/kir-fritzsche/aif490/devel/tissue_analysis/lymphnode_analysis/src/utils/tif2zarr.py \
            --input_dir {OUTPUT_DIR_COMBINED} \
            --zarr_path {output} \
            --filter foreground &> {log}
        """
